apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'ch.raffael.antlr4'

buildscript {
    ext.protobuf_version = '3.+'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group:'ch.raffael.gradlePlugins.antlr4', name:'gradle-antlr4-plugin', version:'1.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'idea'
idea {
    module {
        outputDir = file("$buildDir/classes/main")
        testOutputDir = file("$buildDir/classes/test")
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'

sourceSets.main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.1.0'
    compile 'commons-cli:commons-cli:1.3'
    compile 'com.google.guava:guava:22.0'
    compile 'net.harawata:appdirs:1.0.0'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'org.antlr:antlr4-runtime:4.5'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

def pathToMain = 'com.jetbrains.jetpad.vclang.frontend'
def pathToPrelude = 'com.jetbrains.jetpad.vclang.prelude'

task jarDep(type: Jar) {
    description 'Assembles a jar archive containing the main classes and all dependencies.'
    group 'Build'
    manifest {
        attributes 'Main-Class': "${pathToMain}.ConsoleMain"
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}


// Prelude stuff

def preludeOutputDir = "$buildDir/classes/main"

task copyPrelude(type:Copy) {
    from 'lib/Prelude.vc'
    into "$preludeOutputDir/lib"
}

task prelude(type:JavaExec) {
    description 'Builds the prelude cache.'
    group 'Build'
    main = "${pathToPrelude}.PreludeBinaryGenerator"
    classpath = sourceSets.main.runtimeClasspath
    args = [preludeOutputDir]
}

prelude.dependsOn copyPrelude
jar.dependsOn prelude
jarDep.dependsOn prelude

/* Prelude is copied in the copyPrelude task
jar {
    from('lib') {
        into 'lib'
        include 'Prelude.vc'
    }
}
*/


// Some configs

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.1.0'
    }
}

antlr4 {
    group 'Source'
    description 'Generates antlr4 sources.'

    version = '4.5'
    listener = false
    visitor = true
}
