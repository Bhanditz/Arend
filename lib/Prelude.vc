\static \data Nat
    | zero
    | suc Nat

\static \data Lvl

\static \data CNat
    | inf
    | fin Nat

\static \data I
    | left
    | right

\static \data Path (A : I -> \Type) (a : A left) (a' : A right)
    | path (\Pi (i : I) -> A i)

\static \function \infix 1
(=) {A : \Type} (a a' : A) => Path (\lam _ => A) a a'

\static \function \infixl 9
(@) {A : I -> \Type} {a : A left} {a' : A right} (p : Path A a a') (i : I) : A i <= \elim i
    | left => a
    | right => a'
    | _ <= \elim p
        | path f => f i

\static \function
coe (A : I -> \Type) (a : A left) (i : I) : A i <= \elim i
    | left => a

\static \function
iso {A B : \Type} (f : A -> B) (g : B -> A) (p : \Pi (x : A) -> g (f x) = x) (q : \Pi (y : B) -> f (g y) = y) (i : I) : \Type <= \elim i
    | left => A
    | right => B

\static \data TrP {lp : Lvl} {lh : CNat} (A : \Type (lp,lh))
    | inP A
    | truncP (a a' : TrP A) I
  \with
    | truncP a _  left  => a
    | truncP _ a' right => a'

\static \data TrS {lp : Lvl} {lh : CNat} (A : \Type (lp,lh))
    | inS A
    | truncS (a a' : TrS A) (p p' : TrS A) I I
  \with
    | truncS _ _ p _  i left  => p @ i
    | truncS _ _ _ p' i right => p' @ i
    | truncS a _ _ _  left  _ => a
    | truncS _ a' _ _ right _ => a'
